* SPICE Model of memristor (slow time)

* Created by Ashira Ip
* Last Update: 24/09/2024

* Connections:
* TE:  Top electrode
* BE:  Bottom electrode
* XSV: External connection to plot state variable
*      that is not used otherwise

.SUBCKT MEM_A_SLOW TE BE XSV

* Rl: Minimum device resistance
* Rh: Maximum device resistance

* a, b: Variables for tau when device in high resistance state

* thp: threshold for tau switching in x when V > 0
* ap1: Variable for tau when x < thp and V > 0
* bp1: Variable for tau when x < thp and V > 0

* thn: threshold for tau switching in x when V < 0
* an1: Variable for tau1 when x < thn and V < 0
* bn1: Variable for tau1 when x < thn and V < 0

* x0: initial x

.params Rl=2245 Rh=85755 a=0.1894 b=0.2396 thp=0.04 ap1=-1.4730 bp1=1.5680
+thn=0.01 an1=1.2502 bn1=0.0527 x0=1.0

* Function R(V(t)) - Describes the device threshold
.func R(V) = Rl+(Rh-Rl)*V

* Function F(V(t),x(t)) - Describes the SV motion
.func F(V1,V2) = IF(V1>0, IF(V2>thp, V2-V2/10**(-(V1-a)/b),IF(V2<0.000001,0,V2-V2/10**(-(V1-ap1)/bp1))),
+IF(V1<0,IF(V2<thn, V2+(1-V2)/10**(-(-V1-an1)/bn1),IF(V2>0.999999, 1,V2+(1-V2)/10**(-(-V1-a)/b))),V2))
*.func F(V1,V2) = IF(V1>0,IF(V2>thp,2,1),IF(V2))

* Memristor I-V Relationship
.func IVRel(V1,V2) = V1/R(V2)

* Circuit to determine state variable
* dx/dt = F(V(t),x(t))*R(V(t))
Cx XSV 0 {1}
.ic V(XSV) = x0
Gx 0 XSV value={F(V(TE,BE),V(XSV,0))}

* Current source for memristor IV response
Gmem TE BE value={IVRel(V(TE,BE),V(XSV,0))}

.ENDS MEM_A_SLOW